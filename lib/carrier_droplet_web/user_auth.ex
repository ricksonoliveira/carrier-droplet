defmodule CarrierDropletWeb.UserAuth do
  @moduledoc """
  Handles user authentication for LiveViews.
  """

  use CarrierDropletWeb, :verified_routes

  import Phoenix.Component
  import Phoenix.LiveView

  alias CarrierDroplet.Accounts

  def on_mount(:ensure_authenticated, _params, session, socket) do
    socket = assign_current_user(socket, session)

    if socket.assigns.current_user do
      {:cont, socket}
    else
      socket =
        socket
        |> put_flash(:error, "You must be logged in to access this page.")
        |> redirect(to: ~p"/")

      {:halt, socket}
    end
  end

  defp assign_current_user(socket, session) do
    case session do
      %{"user_id" => user_id} ->
        assign_new(socket, :current_user, fn ->
          Accounts.get_user(user_id)
        end)

      %{} ->
        assign_new(socket, :current_user, fn -> nil end)
    end
  end
end
